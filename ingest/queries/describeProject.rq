PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX dco: <http://info.deepcarbon.net/schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vitro-pub: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX obo: <http://purl.obolibrary.org/obo/>

DESCRIBE ?project ?dcoid ?mostSpecificType ?mainImage ?thumbImage ?thumbnail ?description ?leader ?community ?portalGroup ?submittedBy ?locationLabel ?role ?participant ?contributingRole ?contributor ?dateTimeInterval ?grant ?start ?end ?startDateTime ?endDateTime ?reportingYear ?pu
WHERE {
  ?project dco:hasDcoId ?dcoid .
  ?project vitro:mostSpecificType ?mostSpecificType .
  ?project vivo:description ?description .
  OPTIONAL { ?project vivo:hasFundingVehicle ?grant }
  OPTIONAL { ?project dco:fieldworkLeader ?leader }
  OPTIONAL { ?project dco:associatedDCOCommunity ?community }
  OPTIONAL { ?project dco:associatedDCOPortalGroup ?portalGroup }
  OPTIONAL { ?project dco:submittedBy ?submittedBy }
  OPTIONAL { ?project dco:hasProjectUpdate ?pu .
    ?pu dco:forReportingYear ?reportingYear
  }
  OPTIONAL { ?project vivo:dateTimeInterval ?dateTimeInterval .
    OPTIONAL { ?dateTimeInterval vivo:start ?start .
      ?start vivo:dateTime ?startDateTime
    }
    OPTIONAL { ?dateTimeInterval vivo:end ?end .
      ?end vivo:dateTime ?endDateTime
    }
  }
  OPTIONAL { ?project vitro-pub:mainImage ?mainImage .
    ?mainImage vitro-pub:thumbnailImage ?thumbImage .
    ?thumbImage vitro-pub:downloadLocation ?thumbnail }
  OPTIONAL { ?project dco:physicalLocation ?location .
    ?location dco:hasLongitude ?longitude .
    ?location dco:hasLatitude ?latitude .
    ?location rdfs:label ?locationLabel .
  }
  OPTIONAL { ?project obo:BFO_0000055 ?role .
    ?role obo:RO_0000052 ?participant .
    ?participant a foaf:Person .
    OPTIONAL { ?participant vivo:hasResearchArea ?researchArea }
    OPTIONAL { ?participant dco:inOrganization ?participantOrganization }
  }
  OPTIONAL { ?project vivo:contributingRole ?contributingRole .
    ?contributingRole obo:RO_0000052 ?contributor .
    ?contributor a foaf:Person .
    OPTIONAL { ?contributor vivo:hasResearchArea ?researchArea }
    OPTIONAL { ?contributor dco:inOrganization ?participantOrganization }
  }
}
