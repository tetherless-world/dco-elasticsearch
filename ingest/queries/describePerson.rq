PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd:   <http://www.w3.org/2001/XMLSchema#>
PREFIX owl:   <http://www.w3.org/2002/07/owl#>
PREFIX dco: <http://info.deepcarbon.net/schema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX vivo: <http://vivoweb.org/ontology/core#>
PREFIX vitro: <http://vitro.mannlib.cornell.edu/ns/vitro/0.7#>
PREFIX vitro-pub: <http://vitro.mannlib.cornell.edu/ns/vitro/public#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX bibo: <http://purl.org/ontology/bibo/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>

DESCRIBE ?person ?dcoid ?mostSpecificType ?org ?dco_community ?teamrole ?team ?main_image ?thumb_image ?thumbnail ?research_area ?vcard ?email ?name ?position ?affiliation ?homeCountry
WHERE {
  ?person a foaf:Person .
  ?person vitro:mostSpecificType ?mostSpecificType .
  OPTIONAL { { ?person dco:hasDcoId ?dcoid } UNION { ?dcoid dco:dcoIdFor ?person } }
  OPTIONAL { ?person obo:ARG_2000028 ?vcard
    OPTIONAL { ?vcard vcard:hasEmail ?email }
    OPTIONAL { ?vcard vcard:hasName ?name }
  }
  OPTIONAL { ?person vivo:relatedBy ?position .
             ?position a vivo:Position .
             ?position vivo:relates ?affiliation .
             ?affiliation a foaf:Organization }
  OPTIONAL { ?person dco:inOrganization ?org }
  OPTIONAL { ?person dco:associatedDCOCommunity ?dco_community }
  OPTIONAL { ?person obo:RO_0000053 ?teamrole .
             ?teamrole a vivo:MemberRole .
             ?teamrole vivo:roleContributesTo ?team .
             ?team a dco:Team . }
  OPTIONAL { ?person dco:associatedDCOTeam ?team }
  OPTIONAL { ?person vitro-pub:mainImage ?main_image .
             ?main_image vitro-pub:thumbnailImage ?thumb_image .
             ?thumb_image vitro-pub:downloadLocation ?thumbnail }
  OPTIONAL { ?person vivo:hasResearchArea ?research_area }
  OPTIONAL { ?person dco:homeCountry ?homeCountry }
}
